apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven-publish'

group = 'org'
version = project.version

import org.tmatesoft.svn.core.wc.*

repositories {
    mavenCentral()
    mavenLocal()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.tmatesoft.svnkit', name: 'svnkit', version: '1.8.11'
    }
}

def getSvnRevision(){
    ISVNOptions options = SVNWCUtil.createDefaultOptions(true);
    SVNClientManager clientManager = SVNClientManager.newInstance(options);
    SVNStatusClient statusClient = clientManager.getStatusClient();
    SVNStatus status = statusClient.doStatus(projectDir, false);
    SVNRevision revision = status.getRevision();
    return revision.getNumber();
}

//sourceCompatibility = 1.8
//targetCompatibility = 1.8

project.version=repiceaVersion+'.'+getSvnRevision()

println "Building " + rootProject.name + " v" + project.version

println "Using java version " + JavaVersion.current()

sourceSets {
    main {    	
        java {
            srcDirs 'src'            
        }
        
        resources {
			srcDirs= ['src']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }

    test {
        java {
            srcDirs 'test'
        }
                
        resources {
			srcDirs= ['test']
			exclude '**/*.java'
		}
		
		output.resourcesDir = "$buildDir/classes/java/$name"
    }               
}

dependencies {
    implementation 'com.cedarsoftware:json-io:4.12.0'
    implementation 'org.apache.xmlgraphics:batik-dom:1.14'
	implementation 'org.apache.xmlgraphics:batik-svggen:1.14'
    implementation 'net.sf.ucanaccess:ucanaccess:4.0.4'
    
    if(JavaVersion.current() == JavaVersion.VERSION_1_8){
 		implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'   
	}
      
	compile group: 'org.jboss', name: 'jdk-misc', version: '3.Final'     
    
    testImplementation "junit:junit:4.12"    
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true

    // set heap size for the test JVM(s)
    maxHeapSize = "2048m"
    
    exclude '**/ProductionEnvironmentTest.class'
}

task integrationTest(type: Test) {
	description = 'Runs the integration tests.'
    group = 'verification'
	dependsOn jar
	
	include '**/*.class'
	
	testLogging.showStandardStreams = true
    testClassesDirs = sourceSets.test.output.classesDirs
        
    // set classpath to test classes + test dependencies + JAR
    classpath = project.sourceSets.test.output + configurations.testRuntimeClasspath + files(jar.archiveFile) 
    outputs.upToDateWhen { false }
    mustRunAfter test
    maxHeapSize = "2048m"
}

check.dependsOn integrationTest

jar {
	 manifest {
        attributes("Built-By": "Mathieu Fortin",
        		   "Specification-Title": "REpicea Utilitaries",
        		   "Specification-Version": archiveVersion,                   
                   "Specification-Vendor": "REpicea",
                   "Implementation-Title": "repicea",
                   "Implementation-Version": archiveVersion,
                   "Implementation-Vendor": "REpicea",
                   )
    }
    
	into ('/') {
		from files(['./README.txt', './COPYING.LESSER.txt'])			
	}
	
	// include all java files from main sourceset into the jar file (resources are already included)
	from sourceSets.main.allJava		
}

task deleteInstallFolder(type: Delete) {
  delete "${installDir}"  
}

task install(type: Copy) {
	description = 'Copies the output jar to installation folder.'
	group = 'build'
	dependsOn jar,deleteInstallFolder
	from configurations.default
	from jar
	into "${installDir}"
}

publishing {
    publications {
        repicea(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
    }
}